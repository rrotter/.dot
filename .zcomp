## fpath ##
if type brew &>/dev/null; then
  fpath+=$(brew --prefix)/share/zsh/site-functions
fi

## compinit ##
autoload -Uz +X bashcompinit && bashcompinit # bash compat needed for some utils :(
autoload -Uz compinit && compinit
if type tk &>/dev/null; then
  complete -C tk_completer tk
fi

## settings ##
zstyle ':completion:*' special-dirs '..'
zstyle ':completion:*' ignore-parents pwd

# ignore rarely used commands that conflict w/ common commands
function () {
  local _ignore=''
  if [[ `uname -o` = Darwin ]]; then
    # kerberos
    _ignore=${_ignore}'|kadmin*|kcc|kdcsetup|kdestroy|kgetcred|kinit|klist|kpasswd|krb*|ktutil|kswitch'
    # kext
    _ignore=${_ignore}'|kcditto|kext*|klist_cdhashes|kmutil'
    # java
    _ignore=${_ignore}'|keytool'
    # misc deprecated junk
    _ignore=${_ignore}'|DirectoryService|DeRez|ResMerger|Rez|SetFile|SplitForks'
    _ignore=${_ignore}'|ppdc|ppdhtml|ppdi|ppdmerge|ppdpo'
    # other junk, not deprecated, but mostly useless
    _ignore=${_ignore}'|hidutil|hiutil|tkcon|tkmib|tkpp*'
    # other things I don't use directly, but for whatever reason want to keep installed
    _ignore=${_ignore}'|apptainer.lima|docker.lima|kubectl.lima|nerdctl.lima|podman.lima'
  fi
  # brew and kubectl extensions (doesn't affect use as subcommands)
  _ignore=${_ignore}'|brew-*|kubectl-*'
  # bash completers
  _ignore=${_ignore}'|aws_completer|tk_completer'
  # zsh variables
  _ignore=${_ignore}'|histchars|historywords'

  zstyle ':completion::complete:-command-:*:*' ignored-patterns $_ignore
}

# don't autocomplete subcommands for setting up autocomplete ðŸ¤¦
for cmd in gh glow helm k9s kubectl limactl; do
  if type _${cmd} &>/dev/null; then
    zstyle ":completion::complete:${cmd}:*:*" ignored-patterns 'completion'
  fi
done
